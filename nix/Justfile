# Nix configuration task runner

# Default recipe
default:
    @just --list

# One-off: patch /etc/nix/nix.conf with cache trust settings (for bootstrap cases)
hack-nix-conf-caches:
    #!/usr/bin/env bash
    set -euo pipefail

    conf="/etc/nix/nix.conf"
    tmp="$(mktemp)"
    trap 'rm -f "$tmp" "$tmp.new"' EXIT

    sudo cp "$conf" "$tmp"
    sudo cp "$conf" "$conf.bak.$(date +%Y%m%d-%H%M%S)"

    add_value() {
        key="$1"
        value="$2"

        if grep -Eq "^${key}[[:space:]]*=" "$tmp"; then
            current="$(grep -E "^${key}[[:space:]]*=" "$tmp" | head -n1 | sed -E "s/^${key}[[:space:]]*=[[:space:]]*//")"
            if [[ " $current " != *" $value "* ]]; then
                new="$current $value"
                awk -v k="$key" -v n="$new" '
                    BEGIN { done = 0 }
                    $0 ~ "^" k "[[:space:]]*=" && done == 0 { print k " = " n; done = 1; next }
                    { print }
                ' "$tmp" > "$tmp.new"
                mv "$tmp.new" "$tmp"
            fi
        else
            printf '%s = %s\n' "$key" "$value" >> "$tmp"
        fi
    }

    # Needed for daemon/root builds to actually query these caches.
    add_value substituters "https://cache.numtide.com"
    add_value substituters "https://claude-code.cachix.org"
    add_value substituters "https://niri.cachix.org"

    # Keep trusted-substituters aligned for explicit extra-substituter usage.
    add_value trusted-substituters "https://cache.numtide.com"
    add_value trusted-substituters "https://claude-code.cachix.org"
    add_value trusted-substituters "https://niri.cachix.org"

    add_value trusted-public-keys "niks3.numtide.com-1:DTx8wZduET09hRmMtKdQDxNNthLQETkc/yaX7M4qK0g="
    add_value trusted-public-keys "claude-code.cachix.org-1:YeXf2aNu7UTX8Vwrze0za1WEDS+4DuI2kVeWEE4fsRk="
    add_value trusted-public-keys "niri.cachix.org-1:Wv0OmO7PsuocRKzfDoJ3mulSl7Z6oezYhGhR+3W2964="

    sudo install -Dm0644 "$tmp" "$conf"
    if command -v systemctl >/dev/null 2>&1 && systemctl list-unit-files | grep -q '^nix-daemon\\.service'; then
        sudo systemctl restart nix-daemon
    fi

    echo "Updated $conf"
    sudo grep -E '^(substituters|trusted-substituters|trusted-public-keys)[[:space:]]*=' "$conf"

# Switch configuration (auto-detects NixOS vs Home Manager)
switch:
    #!/usr/bin/env bash
    set -euo pipefail
    host=$(hostname | sed 's/\.local$//')
    if [ -f /etc/NIXOS ]; then
        echo "NixOS detected, running nixos-rebuild switch..."
        sudo nixos-rebuild switch --flake ".#$host"
    else
        echo "Standalone Home Manager detected, running home-manager switch..."
        home-manager switch -b bak --flake ".#$host"
    fi

# Build on this machine and deploy/switch on a remote NixOS host
deploy target:
    nixos-rebuild switch --flake ".#{{target}}" --build-host "" --target-host "{{target}}" --sudo

# Dry-run NixOS rebuild (NixOS only)
dry:
    sudo nixos-rebuild dry-run --flake ".#$(hostname)"

# Build and show what changed (NixOS only)
diff:
    #!/usr/bin/env bash
    set -euo pipefail
    host=$(hostname)
    sudo nixos-rebuild build --flake ".#$host" && nvd diff /run/current-system result

# Evaluate config without building (auto-detects NixOS vs Home Manager)
eval:
    #!/usr/bin/env bash
    set -euo pipefail
    host=$(hostname | sed 's/\.local$//')
    if [ -f /etc/NIXOS ]; then
        echo "Evaluating NixOS config for $host..."
        nix eval ".#nixosConfigurations.$host.config.system.build.toplevel" --no-write-lock-file > /dev/null
    else
        echo "Evaluating Home Manager config for $host..."
        nix eval ".#homeConfigurations.$host.activationPackage" --no-write-lock-file > /dev/null
    fi
    echo "Config is valid"

# Evaluate all host configurations
eval-all:
    #!/usr/bin/env bash
    set -euo pipefail
    failed=0

    echo "==> Evaluating NixOS configurations..."
    for host in thinkpad thrawny-desktop; do
        echo -n "  $host: "
        if nix eval ".#nixosConfigurations.$host.config.system.build.toplevel" --no-write-lock-file > /dev/null 2>&1; then
            echo "ok"
        else
            echo "FAILED"
            failed=1
        fi
    done

    echo ""
    echo "==> Evaluating Home Manager configurations..."
    for host in thrawnym1 thrawny-asahi-air; do
        echo -n "  $host: "
        if nix eval ".#homeConfigurations.$host.activationPackage" --no-write-lock-file > /dev/null 2>&1; then
            echo "ok"
        else
            echo "FAILED"
            failed=1
        fi
    done

    echo ""
    if [ $failed -eq 0 ]; then
        echo "All configurations valid!"
    else
        echo "Some configurations failed!"
        exit 1
    fi

# Update AI tool flake inputs (claude-code-nix, llm-agents) and switch
ai:
    #!/usr/bin/env bash
    set -euo pipefail

    get_version() {
        local v
        v=$("$1" --version 2>/dev/null | head -1)
        echo "${v:-not installed}"
    }

    if command -v claude-node &>/dev/null; then
        claude_bin=claude-node
    else
        claude_bin=claude
    fi

    old_claude=$(get_version "$claude_bin")
    old_codex=$(get_version codex)
    old_pi=$(get_version pi)

    nix flake update claude-code-nix llm-agents

    if git diff --quiet -- flake.lock; then
        echo "No updates available."
        exit 0
    fi

    just switch

    new_claude=$(get_version "$claude_bin")
    new_codex=$(get_version codex)
    new_pi=$(get_version pi)

    changed=false
    echo ""
    echo "==> Version changes:"
    if [ "$old_claude" != "$new_claude" ]; then
        echo "  claude: $old_claude -> $new_claude"
        changed=true
    fi
    if [ "$old_codex" != "$new_codex" ]; then
        echo "  codex: $old_codex -> $new_codex"
        changed=true
    fi
    if [ "$old_pi" != "$new_pi" ]; then
        echo "  pi: $old_pi -> $new_pi"
        changed=true
    fi
    if [ "$changed" = false ]; then
        echo "  (no version changes detected)"
        echo "Reverting flake.lock."
        git checkout -- flake.lock
        exit 0
    fi

    git add flake.lock
    git commit -m "chore(nix): update AI flake inputs" -- flake.lock

# Clean up old generations (keeps last 6) and optimize store
clean:
    #!/usr/bin/env bash
    set -euo pipefail
    echo "==> Checking /nix/store size before cleanup..."
    du -sh /nix/store 2>&1 | grep -v 'Permission denied' || true

    echo ""
    echo "==> Removing old user profile generations (keeping last 6)..."
    nix-env --delete-generations +6

    echo ""
    echo "==> Removing old system generations (keeping last 6)..."
    sudo nix-env -p /nix/var/nix/profiles/system --delete-generations +6 || true

    echo ""
    echo "==> Running garbage collection..."
    nix-collect-garbage

    echo ""
    echo "==> Optimizing Nix store (deduplicating files)..."
    nix store optimise

    echo ""
    echo "==> Cleanup complete! Final /nix/store size:"
    du -sh /nix/store 2>&1 | grep -v 'Permission denied' || true

# Format Nix files
fmt:
    nix fmt .

# Lint Nix files
lint:
    statix check .

# Format, lint, and evaluate current host
check: fmt lint eval

# Format, lint, and evaluate all hosts
check-all: fmt lint eval-all
