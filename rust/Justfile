# Rust workspace task runner
# Packages: bash-validator, tmux-fzf-switcher, voice, agent-switch

# Default recipe
default:
    @just --list

# Build a package (or all)
build *PKG:
    cargo build --release {{ if PKG != "" { "-p " + PKG } else { "--workspace" } }}

# Run a package
run PKG:
    cargo run --release -p {{ PKG }}

# Watch and rebuild a package on changes
watch PKG *ARGS:
    watchexec -w {{ PKG }}/src -e rs --restart -- cargo run -p {{ PKG }} {{ ARGS }}

# Watch agent-switch tmux daemon
watch-tmux:
    RUST_LOG=debug watchexec -w agent-switch/src -e rs --restart -- cargo run -p agent-switch -- serve

# Watch agent-switch niri daemon
watch-niri:
    RUST_LOG=debug watchexec -w agent-switch/src -e rs --restart -- cargo run -p agent-switch --features niri -- niri

# Watch voice daemon (provider: openai or groq)
watch-voice provider="groq":
    RUST_LOG=debug VOICE_PROVIDER={{ provider }} watchexec -w voice/src -e rs --restart -- cargo run -p voice -- serve


# Build with nix (clean build)
nix PKG:
    nix build path:../nix#{{ PKG }}

# Install binaries to ~/.cargo/bin (in PATH)
install *PKG:
    if [ -n "{{ PKG }}" ]; then \
        cargo install --path {{ PKG }} --locked --force; \
    else \
        cargo install --path bash-validator --locked --force; \
        cargo install --path voice --locked --force; \
        cargo install --path agent-switch --locked --force; \
    fi


# Run clippy with auto-fix
clippy:
    cargo clippy --fix --allow-dirty --allow-staged --release --workspace

# Run tests
test:
    cargo test --release --workspace

# Format code
fmt:
    cargo fmt --all
